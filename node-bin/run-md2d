#!/usr/bin/env coffee

fs          = require "fs"
md2dNodeApi = require "../src/helpers/md2d/md2d-node-api"

runSimulation = (inFilePath, output, totalTime) ->
  console.log """\nModel file: #{inFilePath}
                 Output: #{output}
                 Integration time: #{totalTime}
                 time\tKE\tTE"""

  modelJSON = JSON.parse fs.readFileSync(inFilePath).toString()

  # Create MD2D modeler.
  model = md2dNodeApi.Modeler modelJSON
  model.resetTime()

  if output == 'stdout'
    out = process.stdout.fd
  else
    out = fs.openSync output, 'w'
    console.log "Writing data to a file..."

  while (model.get('time') <= totalTime)
    str =  "#{model.get 'time'}\t"
    str += "#{model.get('kineticEnergy').toFixed(4)}\t"
    str += "#{(model.get('kineticEnergy') + model.get('potentialEnergy')).toFixed(4)}\n"
    fs.writeSync out, str
    # Integrate
    model.tick()
  fs.closeSync out

# Begin script.

argv = require('optimist')
  .usage('$0 -i [path] -o [path or stdout] -i [num]')
  .options 'i'
    alias: 'input'
    demand: true
    string: true
    describe: 'Model JSON file'
  .options 'o'
    alias: 'output'
    default: 'stdout'
    string: true
    describe: 'Output file or stdout'
  .options 't'
    alias: 'time'
    default: 100
    describe: 'Integration time'
  .argv

runSimulation argv.input, argv.output, argv.time
